#version 430 core

layout(binding = 0, rgba32f) uniform image2D framebuffer;

//Uniforms

uniform vec3 eye;
uniform vec3 ray00;
uniform vec3 ray01;
uniform vec3 ray10;
uniform vec3 ray11;

struct rayObj
{
  vec3 dir;
  vec3 ori;
};

//Object types

struct material
{
  vec3 color;
};

struct box {
  vec3 min;
  vec3 max;
  material mat;
};

struct sphere{
  vec3 pos;
  float radius;
  material mat;
};


//Object definitions
#define MAX_SCENE_BOUNDS 1000.0
#define NUM_BOXES 1
#define NUM_SPHERES 2

const box boxes[] = {
  /* The ground */
 box(vec3(-5.0, -0.1, -5.0), vec3(5.0, 0.0, 5.0), material(vec3(0.7)))
  /* box in the middle */
  //{vec3(-0.5, 0.0, -0.5), vec3(0.5, 1.0, 0.5)}
};

const sphere spheres[] = {
  /* The ground */
  /* sphere in the middle */
 sphere(vec3(-0.5, 1.0, -0.5), 1.0, material(vec3(0.0, 1.0, 0.0))),
 sphere(vec3(-2.5, 1.0, -0.5), 1.0, material(vec3(1.0, 0.0, 0.0)))
};

struct hitinfo {
  vec2 lambda;
  int bi;
};

// box intersections

bool intersectBox(rayObj ray, const box b, out float t) {
  vec3 tMin = (b.min - ray.ori) / ray.dir;
  vec3 tMax = (b.max - ray.ori) / ray.dir;
  vec3 t1 = min(tMin, tMax);
  vec3 t2 = max(tMin, tMax);
  float tNear = max(max(t1.x, t1.y), t1.z);
  float tFar = min(min(t2.x, t2.y), t2.z);
  if (tNear > 0.0 && tNear < tFar) //Ray hit the box
  {
    t = tNear;
    return true;
  }
  return false;
}

bool intersectSphere(rayObj ray, const sphere s, out float t)
{
    vec3 oc = ray.ori - s.pos;
    float a = dot(ray.dir, ray.dir);
    float b = dot(oc, ray.dir);
    float c = dot(oc, oc) - s.radius * s.radius;
    float discriminant = b * b - a * c;

    if (discriminant > 0.0) //Ray hit the sphere
    {
        float t1 = (-b - sqrt(b * b - a * c)) / a;
        float t2 = (-b + sqrt(b * b - a * c)) / a;

        t = min(t1, t2); //Return the closest hit

        return true;
    }

    return false;
}

vec4 trace(rayObj ray) {
  vec4 color = vec4(0.0, 0.6, 0.8, 1.0); //Default hit color, sky color

  float tHit = MAX_SCENE_BOUNDS; //Furthest hit possible, at the edge of scene
  float t = 0;
  bool hit = false;

  //Check if the ray intersects with box objects
  for (int i = 0; i < NUM_BOXES; i++) {
     bool hit = intersectBox(ray, boxes[i], t); //Check if the ray hits the box
     if (hit && t > 0 && t < tHit) { //Is the hit closer to the camera than the previous hit?
      tHit = t;
      color = vec4(boxes[i].mat.color, 1.0);
    }
  }

  //Check if the ray intersects with sphere objects
  for (int i = 0; i < NUM_SPHERES; i++) {
    bool hit = intersectSphere(ray, spheres[i], t); //Check if the ray hits the sphere
    if (hit && t > 0 && t < tHit) { //Is the hit closer to the camera than the previous hit?
      tHit = t;
      color = vec4(spheres[i].mat.color, 1.0);
    }
  }

  return color; //Return the color value of this pixel
}

layout (local_size_x = 16, local_size_y = 8) in;
void main(void) {
  ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
  ivec2 size = imageSize(framebuffer);
  if (pix.x >= size.x || pix.y >= size.y) {
    return;
  }
  vec2 pos = vec2(pix) / vec2(size.x - 1, size.y - 1);
  vec3 dir = mix(mix(ray00, ray01, pos.y), mix(ray10, ray11, pos.y), pos.x);
  rayObj ray = rayObj(dir, eye);
  vec4 color = trace(ray);
  imageStore(framebuffer, pix, color);
}
