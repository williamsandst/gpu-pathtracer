#version 430 core

layout(binding = 0, rgba32f) uniform image2D framebuffer;

//Uniforms

uniform vec3 eye;
uniform vec3 ray00;
uniform vec3 ray01;
uniform vec3 ray10;
uniform vec3 ray11;

//Support structs
struct rayObj
{
  vec3 dir;
  vec3 ori;
};

struct material
{
  vec4 color;
  float reflection;
};

struct hitInfo
{
  float t; //Distance along ray
  int objIndex; //Normal of the hit object
  int objType; //Box 1, Sphere 2
  material mat;
};

//Object types
struct box {
  vec3 min;
  vec3 max;
  material mat;
};

struct sphere{
  vec3 pos;
  float radius;
  material mat;
};

//Raycasting settings
#define MAX_RAY_DEPTH 2

//Object definitions
#define MAX_SCENE_BOUNDS 1000.0
#define NUM_BOXES 0
#define NUM_SPHERES 2

const box boxes[] = {
  /* The ground */
 box(vec3(-5.0, -0.1, -5.0), vec3(5.0, 0.0, 5.0), material(vec4(0.7, 0.7, 0.7, 1.0), 1))
  /* box in the middle */
  //{vec3(-0.5, 0.0, -0.5), vec3(0.5, 1.0, 0.5)}
};

const sphere spheres[] = {
  /* The ground */
  /* sphere in the middle */
 sphere(vec3(-0.5, 1.0, -0.5), 1.0, material(vec4(0.0, 1.0, 0.0, 1.0), 1)),
 sphere(vec3(-10.5, 1.0, -0.5), 4.0, material(vec4(1.0, 0.0, 0.0, 1.0), 1))
};

//Intersect functions. Determines if a ray hits a certain object.
//t is the location of the hit. Can get it as a point through the function: ray.ori + ray.dir * t

bool intersectBox(rayObj ray, const box b, out float t) {
  vec3 tMin = (b.min - ray.ori) / ray.dir;
  vec3 tMax = (b.max - ray.ori) / ray.dir;
  vec3 t1 = min(tMin, tMax);
  vec3 t2 = max(tMin, tMax);
  float tNear = max(max(t1.x, t1.y), t1.z);
  float tFar = min(min(t2.x, t2.y), t2.z);
  if (tNear > 0.0 && tNear < tFar) //Ray hit the box
  {
    t = tNear;
    return true;
  }
  return false;
}

bool intersectSphere(rayObj ray, const sphere s, out float t)
{
    vec3 oc = ray.ori - s.pos;
    float a = dot(ray.dir, ray.dir);
    float b = dot(oc, ray.dir);
    float c = dot(oc, oc) - s.radius * s.radius;
    float discriminant = b * b - a * c;

    if (discriminant > 0.0) //Ray hit the sphere
    {
        float t1 = (-b - sqrt(b * b - a * c)) / a;
        float t2 = (-b + sqrt(b * b - a * c)) / a;

        t = min(t1, t2); //Return the closest hit

        return true;
    }

    return false;
}

//Various functions relating to the propagation of a ray

vec3 getRayPos(rayObj ray, float t)
{
  return ray.ori + ray.dir * t;
}

vec3 sphereNormal(vec3 point, sphere s)
{
  return normalize(point - s.pos); 
}

rayObj reflect(rayObj ray, vec3 pos, vec3 normal)
{
  vec3 dir = ray.dir - 2 * dot(ray.dir, normal) * normal; 
  return rayObj(normalize(dir), pos);
}

vec4 trace(rayObj ray) {
  int depth = 0;
  hitInfo skyHit = hitInfo(MAX_SCENE_BOUNDS, 0, 0, material(vec4(0.0, 0.6, 0.8, 1.0), 0));
  //hitInfo skyHit = hitInfo(MAX_SCENE_BOUNDS, 0, 0, material(vec4(0.0, 0.0, 0.0, 1.0), 0));

  vec4 color;

  while (depth < MAX_RAY_DEPTH)
  {
    hitInfo hit;
    hit.t = MAX_SCENE_BOUNDS;
    float t = 0;
    bool hasHit = false;
    bool hasHitTemp = false;

    //Check if the ray intersects with box objects
    for (int i = 0; i < NUM_BOXES; i++) {
      hasHitTemp = intersectBox(ray, boxes[i], t); //Check if the ray hits the box
      if (hasHitTemp && t > 0 && t < hit.t) { //Is the hit closer to the camera than the previous hit?
        hit.t = t;
        hit.mat = boxes[i].mat;
        hit.objType = 1;
        hit.objIndex = i;
        hasHit = true;
      }
    }

    //Check if the ray intersects with sphere objects
    for (int i = 0; i < NUM_SPHERES; i++) {
      hasHitTemp = intersectSphere(ray, spheres[i], t); //Check if the ray hits the sphere
      if (hasHitTemp && t > 0 && t < hit.t) { //Is the hit closer to the ray origin than the previous hit?
        hit.t = t;
        hit.mat = spheres[i].mat;
        hit.objType = 2;
        hit.objIndex = i;
        hasHit = true;
      }
    }

    if (hasHit) //If object is hit, calculate new ray
    {
      vec3 pos = getRayPos(ray, hit.t);
      vec3 normal = sphereNormal(pos, spheres[hit.objIndex]);
      ray = reflect(ray, pos, normal);
      if (depth == 0)
        color = hit.mat.color;
      else
        color = mix(color, hit.mat.color, 0.4);
      depth = depth + 1;
    }
    else
    {
      if (depth == 0)
        color = skyHit.mat.color;
      else
        color = mix(color, skyHit.mat.color, 0.2);
      break;
    }
  }

  return color; //Return the color value of this pixel
}

layout (local_size_x = 16, local_size_y = 8) in;
void main(void) {
  ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
  ivec2 size = imageSize(framebuffer);
  if (pix.x >= size.x || pix.y >= size.y) {
    return;
  }
  vec2 pos = vec2(pix) / vec2(size.x - 1, size.y - 1);
  vec3 dir = mix(mix(ray00, ray01, pos.y), mix(ray10, ray11, pos.y), pos.x);
  rayObj ray = rayObj(dir, eye);
  vec4 color = trace(ray);
  imageStore(framebuffer, pix, color);
}
